#!/usr/bin/python3
from os.path import splitext

from SCons.Script import *

Import("*")

RTOS_DIR = Dir("RTOS")
PROJECT_SOURCE_DIR = Dir("src")
PROJECT_HW_DIR= Dir("HW")
PROJECT_LIB_DIR = Dir("lib")
LIBS_DIR = Dir("#/embedded/libs")
FREERTOS_DIR = LIBS_DIR.Dir("FreeRTOS")
CMSIS_DIR = LIBS_DIR.Dir("CMSIS")

BUILD_DIR = Dir("build")

env = Environment(tools=["arm-none-eabi", "chip_config", "st-flash", "openocd"])
rtos_env = Environment(tools=["arm-none-eabi"])

env.Tool("compilation_db")
env["REPO_ROOT_DIR"] = Dir("#")

common_flags = [
    "-mthumb",
]

debug_flags = [
    "-ggdb2",
    "-g2",
    "-Og",
]

release_flags = [
    "-O2",
]

link_flags = [
    "-Wl,--gc-sections,--relax,--print-memory-usage",
    f"-Wl,-Map={BUILD_DIR.File('v1.map').path}",
    "-Wl,--print-gc-sections",
    "--specs=nano.specs",
    "--specs=nosys.specs",
]

as_flags = ["-xassembler-with-cpp"]

c_flags = [
    "-std=c11",
    "-ffunction-sections",
    "-fdata-sections",
    "-fshort-enums",
    "-funsigned-char",
    "-fstack-usage",
    "-nostdlib",
    "-msoft-float",
    "-DUSE_HAL_DRIVER",
    "-Wall",
    "-Wextra",
    "-Werror",
    "-Wfloat-equal",
    "-Wcast-align",
    "-Wlogical-op",
    "-Winline",
    "-Wshadow",
    "-Winit-self",
    "-Wmissing-prototypes",
    "-Wunused-function",
    "-Wpointer-arith",
    "-Wno-type-limits",
    "-Wno-unused-local-typedefs",
]

free_rtos_flags = [
#    "-Wno-missing-prototypes",
#    "-Wno-cast-align",
]

device = GetOption("device")
if not device:
  device = "f4"
  print("Warning: No specified device, using -> " + device)

if device == "f4":
  config_file = "HW/f4mcuConfig.yml"
  chip_file = "site_scons/f4chips.yml"
elif device == "f1":
  config_file = "HW/f1mcuConfig.yml"
  chip_file = "site_scons/f1chips.yml"

chip_config = env.ChipConfig(config_file=config_file, chip_file=chip_file)
if "defines" in chip_config:
    c_flags.append(chip_config["defines"])

if GetOption("release"):
    common_flags += release_flags
else:
    common_flags += debug_flags


env["LINKFLAGS"] = [link_flags + common_flags]

RTOS_INCLUDE_DIRS = [
    #RTOS_DIR.Dir("include"),
    #FREERTOS_DIR.Dir("Source/include/"),
    #CMSIS_DIR.Dir("CMSIS/Core/Include/"),
]

DRIVERS_INCLUDE_DIRS = [
    chip_config["cmsis_includes"],
    chip_config["hal_includes"],
]

LIBS_INCLUDE_DIRS = [
    #LIBS_DIR.Dir("printf"),
]

PROJECT_INCLUDE_DIRS = [
    "include/",
    "HW/include/",
    CMSIS_DIR.Dir("CMSIS/Core/Include/"),
]

OPENOCD_BOARD = "stm32f429discovery"
OPENOCD_INTERFACE = "stlink"

if device == "f4":
  OPENOCD_BOARD = "stm32f429discovery"
  common_flags.append("-mcpu=cortex-m4")
  c_flags.append(["-DF4",
              "-DSTM32F4",
              "-DSTM32F429xx"
  ])
  RTOS_INCLUDE_DIRS.append(FREERTOS_DIR.Dir("Source/portable/GCC/ARM_CM4F/"))
elif device == "f1":
  OPENOCD_BOARD = "stm32f103c8"
  common_flags.append("-mcpu=cortex-m3")
  c_flags.append(["-DF1",
              "-DSTM32F1",
              "-DSTM32F103xB"
  ])
  RTOS_INCLUDE_DIRS.append(FREERTOS_DIR.Dir("Source/portable/GCC/ARM_CM3/"))

paths = (
    RTOS_INCLUDE_DIRS + DRIVERS_INCLUDE_DIRS + PROJECT_INCLUDE_DIRS + LIBS_INCLUDE_DIRS
)

env.Append(
    ASFLAGS=as_flags,
    CPPPATH=paths,
    CCFLAGS=common_flags + c_flags,
    LINKSCRIPT=chip_config["linker_file"],
)

rtos_env.Append(
    CPPPATH=paths,
    CCFLAGS=common_flags + c_flags + free_rtos_flags,
)

# List of component C files

project_source_files = [
    PROJECT_SOURCE_DIR.File("System.c"),
]

project_hw_files = [
    #PROJECT_HW_DIR.File("HW_timebase.c"),
]
project_source_files += project_hw_files

rtos_source_files = [
    #RTOS_DIR.File("FreeRTOSResources.c"),
    #RTOS_DIR.File("FreeRTOS_SWI.c"),
    #FREERTOS_DIR.File("Source/croutine.c"),
    #FREERTOS_DIR.File("Source/event_groups.c"),
    #FREERTOS_DIR.File("Source/list.c"),
    #FREERTOS_DIR.File("Source/queue.c"),
    #FREERTOS_DIR.File("Source/tasks.c"),
    #FREERTOS_DIR.File("Source/timers.c"),
    #FREERTOS_DIR.File("Source/portable/GCC/ARM_CM3/port.c"),
    ## FREERTOS_DIR.File("Source/stream_buffer.c"),
    ## FREERTOS_DIR.File("Source/portable/MemMang/heap_4.c"),
]

platform_source_files = [
    #(
    #    LIBS_DIR.File("printf/printf.c"),
    #    ["-Wno-float-equal", "-DPRINTF_INCLUDE_CONFIG_H"],
    #),
]

# add driver source files
platform_source_files.extend(chip_config["sources"])

#with open("env_dump.txt", "w") as file:
#    env_dumped = env.Dump()
#    for key in env_dumped:
#        file.write(key)


def create_output_file_path(file, new_ext, override_dir=None) -> str:
    if not override_dir:
        path = "/".join(file.path.split("/")[2:-1]) + "/"
    else:
        path = override_dir
    fn = splitext(file.name)[0] + new_ext
    return BUILD_DIR.File(path + fn)


def compile_objects(env, files, extra_flags=[], override_dir=None):
    objs = []
    for i, src_file in enumerate(files):
        object = create_output_file_path(src_file, ".obj", override_dir)
        tar = [object]
        if "-fstack-usage" in c_flags:
            tar.append(create_output_file_path(src_file, '.su', override_dir))
        flags = extra_flags[i] if extra_flags else []
        env.Object(target=tar, source=src_file, CCFLAGS=env["CCFLAGS"]+flags)
        objs.append(object)
    return objs


## actually compile stuff

objs = []  # store objects here

# compile platform files
objs.extend(compile_objects(env, *list(zip(*platform_source_files)), override_dir="embedded/"))

# compile rtos source files
objs.extend(compile_objects(rtos_env, rtos_source_files, override_dir="FreeRTOS/"))

# compile project source files
objs.extend(compile_objects(env, project_source_files))

## done compiling

# setup default target which links all object files, builds the program,
# and generates a HEX file
built = lambda: env.Program("build/v1", objs)
env.Alias("build", built())
env.Depends(built(), env["LINKSCRIPT"])
env.Default("build")

# setup additional targets
env.Alias("cdb", env.CompilationDatabase())
env.Alias("upload", env.flash(source="build/v1.bin"))
env.Alias("openocd", env.openocd_srv(interface=OPENOCD_INTERFACE, board=OPENOCD_BOARD))

# give support for halting on gdb/openocd start
mon_reset_cmd = '-ex "monitor reset'
if GetOption("halt"):
    mon_reset_cmd += 'halt"'
else:
    mon_reset_cmd += '"'

env.Alias(
    "flash-debug",
    [env.flash_dbg(source="build/v1.bin"),
    env.launch_gdb(
        "build/steering_wheel.elf",
        f"-ex 'target extended-remote | {env.openocd_cmd(interface=OPENOCD_INTERFACE, board=OPENOCD_BOARD)}'",
        '-ex "monitor reset halt"',
        '-ex load',
    ),]
)
env.Alias(
    "openocd-gdb",
    env.launch_gdb(
        "build/steering_wheel.elf",
        f"-ex 'target extended-remote | {env.openocd_cmd(interface=OPENOCD_INTERFACE, board=OPENOCD_BOARD)}'",
        mon_reset_cmd,
        '-ex load',
    ),
)
env.Alias(
    "gdb",
    env.launch_gdb(
        "build/steering_wheel.elf",
        '-ex "target extended-remote localhost:3333"',
        mon_reset_cmd,
        '-ex load',
    ),
)